//@version=6
indicator("Market Sessions - By Leviathan", overlay = true, max_boxes_count = 500, max_labels_count = 500, max_lines_count = 500)

ms_group = 'Market Sessions Indicator'
ms_border_width = 1
ms_border_style = line.style_dashed

ms_show = input.bool(false, title = "Enable", group = ms_group)

ms_tokyo_show      = input.bool(true, '', inline='ms_tokyo', group=ms_group)
ms_tokyo_label     = input.string('Tokyo', '', inline='ms_tokyo', group=ms_group)
ms_tokyo_color     = input.color(color.rgb(255, 153, 0, 95), '' , inline='ms_tokyo', group=ms_group)
ms_tokyo_time      = input.session(defval="0000-0900", title='     ', inline='ms_tokyo2', group=ms_group)

ms_london_show     = input.bool(true, '', inline='ms_london', group=ms_group)
ms_london_label    = input.string('London', '', inline='ms_london', group=ms_group)
ms_london_color    = input.color(color.rgb(76, 175, 79, 90), '' , inline='ms_london', group=ms_group)
ms_london_time     = input.session(defval="0700-1600", title='     ', inline='ms_london2', group=ms_group)

ms_newyork_show    = input.bool(true, title='', inline='ms_newyork', group=ms_group)
ms_newyork_label   = input.string('New York', '', inline='ms_newyork', group=ms_group)
ms_newyork_color   = input.color(color.rgb(33, 149, 243, 90), '', inline='ms_newyork', group=ms_group)
ms_newyork_time    = input.session(defval="1300-2200", title='     ', inline='ms_newyork2', group=ms_group)

ms_sydney_show     = input.bool(false, title='', inline='ms_sydney', group=ms_group)
ms_sydney_label    = input.string('Sydney', '', inline='ms_sydney', group=ms_group)
ms_sydney_color    = input.color(color.rgb(164, 97, 187, 90), '', inline='ms_sydney', group=ms_group)
ms_sydney_time     = input.session(defval="2100-0600", title='     ', inline='ms_sydney2', group=ms_group)

ms_merge_sessions  = input.bool(false, 'Merge Overlaps', group = ms_group)
ms_hide_weekends   = input.bool(true, 'Hide Weekends', group = ms_group)
ms_label_size      = input.string('Normal', 'Label Size', options = ['Auto', 'Tiny', 'Small', 'Normal', 'Large', 'Huge'], group=ms_group)
ms_display_type    = input.string('Boxes', 'Display Type', options = ['Boxes', 'Zones','Timeline'], group=ms_group)
ms_days_back       = input.float(150, 'Lookback (Days)', group=ms_group, tooltip= 'This inputs defines the lookback period for plotting sessions. Eg. If it is set to 1, only the sessions of the past day will appear')


ms_get_time_range(session_time) =>
    ms_hide_weekends ? session_time+":123456" : session_time+":1234567"

ms_get_label_size(size) =>
    switch size
        'Auto'   => size.auto
        'Tiny'   => size.tiny
        'Small'  => size.small
        'Normal' => size.normal
        'Large' => size.large
        'Huge' => size.huge
        
ms_in_session(session_time) =>
    not na(time(timeframe.period, session_time, "UTC")) and timeframe.isintraday


ms_update_box(session_box, session_label_obj, session_high_price, session_low_price) =>
    box.set_top(session_box, session_high_price)
    box.set_bottom(session_box, session_low_price)
    box.set_right(session_box, bar_index + 1)
    label.set_x(session_label_obj, (box.get_left(session_box) + box.get_right(session_box)) / 2)
    label.set_y(session_label_obj, session_high_price)

var ms_tokyo_high_price     = 0.0, var ms_tokyo_low_price      = 0.0, var ms_tokyo_open_price     = 0.0, var box ms_tokyo_box       = na, var ms_tokyo_t     = 0.0, var label ms_tokyo_label_obj   = na, var line ms_tokyo_oc       = na, var string ms_tokyo_text   = str.tostring(ms_tokyo_label)
var ms_london_high_price    = 0.0, var ms_london_low_price     = 0.0, var ms_london_open_price    = 0.0, var box ms_london_box      = na, var ms_london_t    = 0.0, var label ms_london_label_obj  = na, var line ms_london_oc      = na, var string ms_london_text  = str.tostring(ms_london_label)
var ms_newyork_high_price   = 0.0, var ms_newyork_low_price    = 0.0, var ms_newyork_open_price   = 0.0, var box ms_newyork_box     = na, var ms_newyork_t   = 0.0, var label ms_newyork_label_obj = na, var line ms_newyork_oc     = na, var string ms_newyork_text = str.tostring(ms_newyork_label)
var ms_sydney_high_price    = 0.0, var ms_sydney_low_price     = 0.0, var ms_sydney_open_price    = 0.0, var box ms_sydney_box      = na, var ms_sydney_t    = 0.0, var label ms_sydney_label_obj  = na, var line ms_sydney_oc      = na, var string ms_sydney_text  = str.tostring(ms_sydney_label)
var ms_in_range = false
var ms_in_london_var = false
var ms_in_newyork_var = false
var ms_in_sydney_var = false
var ms_in_tokyo_var = false
// Creating variables for alternative Sessions Box top and bottom (used for merging sessions)
var float ms_tokyo_high_m   = 0, var float ms_tokyo_low_m    = 0, var float ms_london_high_m  = 0, var float ms_london_low_m   = 0, var float ms_newyork_high_m = 0, var float ms_newyork_low_m  = 0, var float ms_sydney_high_m  = 0, var float ms_sydney_low_m   = 0
if ms_show
    var ms_tokyo_time_range   = ms_get_time_range(ms_tokyo_time)
    var ms_london_time_range  = ms_get_time_range(ms_london_time)
    var ms_newyork_time_range = ms_get_time_range(ms_newyork_time)
    var ms_sydney_time_range  = ms_get_time_range(ms_sydney_time)


    ms_mspd        = 24 * 60 * 60 * 1000
    ms_last_bar_date = timestamp(year(timenow), month(timenow), dayofmonth(timenow), hour(timenow), minute(timenow), second(timenow))
    ms_this_bar_date = timestamp(year, month, dayofmonth, hour, minute, second)
    ms_days_left    = math.abs(math.floor((ms_last_bar_date - ms_this_bar_date) / ms_mspd))
    ms_in_range     := ms_days_left < ms_days_back

    ms_in_tokyo_var        := ms_in_session(ms_tokyo_time_range)
    ms_tokyo_start     = ms_in_tokyo_var  and not ms_in_tokyo_var[1]
    ms_in_london_var       := ms_in_session(ms_london_time_range)
    ms_london_start    = ms_in_london_var and not ms_in_london_var[1]
    ms_in_newyork_var      := ms_in_session(ms_newyork_time_range)
    ms_newyork_start   = ms_in_newyork_var and not ms_in_newyork_var[1]
    ms_in_sydney_var       := ms_in_session(ms_sydney_time_range)
    ms_sydney_start    = ms_in_sydney_var and not ms_in_sydney_var[1]

    // Settings high, low, open at the beggining of the session
    if ms_tokyo_start
        ms_tokyo_high_price   := high
        ms_tokyo_low_price    := low
        ms_tokyo_open_price   := open
    if ms_london_start
        ms_london_high_price  := high
        ms_london_low_price   := low
        ms_london_open_price  := open
    if ms_newyork_start
        ms_newyork_high_price := high
        ms_newyork_low_price  := low
        ms_newyork_open_price := open
    if ms_sydney_start
        ms_sydney_high_price  := high
        ms_sydney_low_price   := low
        ms_sydney_open_price  := open

    // Track session's max high and max low during the session
    else if ms_in_tokyo_var
        ms_tokyo_high_price   := math.max(ms_tokyo_high_price, high)
        ms_tokyo_low_price    := math.min(ms_tokyo_low_price, low)
    else if ms_in_london_var
        ms_london_high_price  := math.max(ms_london_high_price, high)
        ms_london_low_price   := math.min(ms_london_low_price, low)
    else if ms_in_newyork_var
        ms_newyork_high_price := math.max(ms_newyork_high_price, high)
        ms_newyork_low_price  := math.min(ms_newyork_low_price, low)
    else if ms_in_sydney_var
        ms_sydney_high_price  := math.max(ms_sydney_high_price, high)
        ms_sydney_low_price   := math.min(ms_sydney_low_price, low)

    // Plotting session boxes at the beginning of each session
    if ms_in_range
        if ms_tokyo_start and ms_tokyo_show
            ms_tokyo_box     := ms_display_type=='Boxes' ? box.new(left=bar_index, top=na, right=na, bottom=na, border_width=ms_border_width, bgcolor = ms_tokyo_color, border_style = ms_border_style, border_color=color.new(ms_tokyo_color, 40)) : na
            ms_tokyo_label_obj   :=  label.new(x=na, y=na, text=ms_tokyo_text, textcolor=color.new(ms_tokyo_color, 40), color=color.rgb(0,0,0,100), size=ms_get_label_size(ms_label_size))
        if ms_london_start and ms_london_show
            ms_london_box    := ms_display_type=='Boxes' ? box.new(left=bar_index, top=na, right=na, bottom=na, border_width=ms_border_width, bgcolor = ms_london_color, border_style = ms_border_style, border_color=color.new(ms_london_color, 40)) : na
            ms_london_label_obj  :=  label.new(x=na, y=na, text=ms_london_text, textcolor=color.new(ms_london_color, 40), color=color.rgb(0,0,0,100), size=ms_get_label_size(ms_label_size))
        if ms_newyork_start and ms_newyork_show
            ms_newyork_box   := ms_display_type=='Boxes' ? box.new(left=bar_index, top=na, right=na, bottom=na, border_width=ms_border_width, bgcolor = ms_newyork_color, border_style = ms_border_style, border_color=color.new(ms_newyork_color, 40)) : na
            ms_newyork_label_obj :=  label.new(x=na, y=na, text=ms_newyork_text, textcolor=color.new(ms_newyork_color, 40), color=color.rgb(0,0,0,100), size=ms_get_label_size(ms_label_size))
        if ms_sydney_start and ms_sydney_show
            ms_sydney_box    := ms_display_type=='Boxes' ? box.new(left=bar_index, top=na, right=na, bottom=na, border_width=ms_border_width, bgcolor = ms_sydney_color, border_style = ms_border_style, border_color=color.new(ms_sydney_color, 40)) : na
            ms_sydney_label_obj  :=  label.new(x=na, y=na, text=ms_sydney_text, textcolor=color.new(ms_sydney_color, 40), color=color.rgb(0,0,0,100), size=ms_get_label_size(ms_label_size))


    // Updating session boxes during sessions
    if ms_in_tokyo_var and ms_in_range
        ms_tokyo_high_price := math.max(ms_tokyo_high_price, high)
        ms_tokyo_low_price := math.min(ms_tokyo_low_price, low)
        ms_update_box(ms_tokyo_box, ms_tokyo_label_obj, ms_tokyo_high_price, ms_tokyo_low_price)
    if ms_in_london_var and ms_in_range
        ms_london_high_price := math.max(ms_london_high_price, high)
        ms_london_low_price := math.min(ms_london_low_price, low)
        ms_update_box(ms_london_box, ms_london_label_obj, ms_london_high_price, ms_london_low_price)
    if ms_in_newyork_var and ms_in_range
        ms_newyork_high_price := math.max(ms_newyork_high_price, high)
        ms_newyork_low_price := math.min(ms_newyork_low_price, low)
        ms_update_box(ms_newyork_box, ms_newyork_label_obj, ms_newyork_high_price, ms_newyork_low_price)
    if ms_in_sydney_var and ms_in_range
        ms_sydney_high_price := math.max(ms_sydney_high_price, high)
        ms_sydney_low_price := math.min(ms_sydney_low_price, low)
        ms_update_box(ms_sydney_box, ms_sydney_label_obj, ms_sydney_high_price, ms_sydney_low_price)

    // Coloring background if display_type=='Zones'
    ms_tokyo_t   := time(timeframe.period, ms_tokyo_time_range)
    ms_london_t  := time(timeframe.period, ms_london_time_range)
    ms_newyork_t := time(timeframe.period, ms_newyork_time_range)
    ms_sydney_t  := time(timeframe.period, ms_sydney_time_range)

bgcolor(ms_display_type == 'Zones' and not ms_merge_sessions and ms_tokyo_show   and ms_in_range and  time == ms_tokyo_t   ? ms_tokyo_color   : na, editable = false)
bgcolor(ms_display_type == 'Zones' and not ms_merge_sessions and ms_london_show  and ms_in_range and  time == ms_london_t  ? ms_london_color  : na, editable = false)
bgcolor(ms_display_type == 'Zones' and not ms_merge_sessions and ms_newyork_show and ms_in_range and  time == ms_newyork_t ? ms_newyork_color : na, editable = false)
bgcolor(ms_display_type == 'Zones' and not ms_merge_sessions and ms_sydney_show  and ms_in_range and  time == ms_sydney_t  ? ms_sydney_color  : na, editable = false)
bgcolor(ms_display_type == 'Zones' and ms_merge_sessions and not ms_in_london_var  and ms_tokyo_show   and ms_in_range and  time == ms_tokyo_t   ? ms_tokyo_color   : na, editable = false)
bgcolor(ms_display_type == 'Zones' and ms_merge_sessions and not ms_in_newyork_var and ms_london_show  and ms_in_range and  time == ms_london_t  ? ms_london_color  : na, editable = false)
bgcolor(ms_display_type == 'Zones' and ms_merge_sessions and not ms_in_sydney_var  and ms_newyork_show and ms_in_range and  time == ms_newyork_t ? ms_newyork_color : na, editable = false)
bgcolor(ms_display_type == 'Zones' and ms_merge_sessions and not ms_in_tokyo_var   and ms_sydney_show  and ms_in_range and  time == ms_sydney_t  ? ms_sydney_color  : na, editable = false)

// Plotting sessions in Timeline form
plotshape(ms_display_type=='Timeline' and (ms_merge_sessions and ms_london_show  ? (ms_tokyo_show   and ms_in_tokyo_var   and not ms_in_london_var)  : ms_tokyo_show   and ms_in_tokyo_var),   style=shape.square, color=ms_tokyo_color,   location = location.bottom, size=size.auto)
plotshape(ms_display_type=='Timeline' and (ms_merge_sessions and ms_newyork_show ? (ms_london_show  and ms_in_london_var  and not ms_in_newyork_var) : ms_london_show  and ms_in_london_var),  style=shape.square, color=ms_london_color,  location = location.bottom, size=size.auto)
plotshape(ms_display_type=='Timeline' and (ms_merge_sessions and ms_sydney_show  ? (ms_newyork_show and ms_in_newyork_var and not ms_in_sydney_var)  : ms_newyork_show and ms_in_newyork_var), style=shape.square, color=ms_newyork_color, location = location.bottom, size=size.auto)
plotshape(ms_display_type=='Timeline' and (ms_merge_sessions and ms_tokyo_show   ? (ms_sydney_show  and ms_in_sydney_var  and not ms_in_tokyo_var)   : ms_sydney_show  and ms_in_sydney_var),  style=shape.square, color=ms_sydney_color,  location = location.bottom, size=size.auto)

// Creating alerts
alertcondition(ms_in_tokyo_var   and not ms_in_tokyo_var[1], 'Tokyo Open', 'The Tokyo Session has started')
alertcondition(ms_in_london_var  and not ms_in_london_var[1], 'London Open', 'The London Session has started')
alertcondition(ms_in_newyork_var and not ms_in_newyork_var[1], 'New York Open', 'The New York Session has started')
alertcondition(ms_in_sydney_var  and not ms_in_sydney_var[1], 'Sydney Open', 'The Sydney Session has started')
alertcondition(high > ms_tokyo_high_price[0]    and ms_in_tokyo_var, 'Tokyo Session - New High', 'New High in Tokyo Session')
alertcondition(high > ms_london_high_price[0]   and ms_in_london_var, 'London Session - New High', 'New High in London Session')
alertcondition(high > ms_newyork_high_price[0]  and ms_in_newyork_var, 'New York Session - New High', 'New High in New York Session')
alertcondition(high > ms_sydney_high_price[0]   and ms_in_sydney_var, 'Sydney Session - New High', 'New High in Sydney Session')
alertcondition(low  > ms_tokyo_low_price[0]     and ms_in_tokyo_var, 'Tokyo Session - New Low', 'New Low in Tokyo Session')
alertcondition(low  > ms_london_low_price[0]    and ms_in_london_var, 'London Session - New Low', 'New Low in London Session')
alertcondition(low  > ms_newyork_low_price[0]   and ms_in_newyork_var, 'New York Session - New Low', 'New Low In New York Session')
alertcondition(low  > ms_sydney_low_price[0]    and ms_in_sydney_var, 'Sydney Session - New Low', 'New Low In Sydney Session')

